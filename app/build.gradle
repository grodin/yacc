buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        // annotation processing
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
        // Android unmocking
        classpath 'de.mobilej.unmock:UnMockPlugin:0.1.1'

    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'de.mobilej.unmock'

println "${rootProject.projectDir}"

dependencies {
    compile project(':common')
    compile project(':data')
    compile project(':domain')

    // Debug only dependencies
    debugCompile project(':debug')
    debugCompile 'org.lucasr.dspec:dspec:0.1.1'

    // TODO: remove release dependency on debug project
    releaseCompile project(':debug')

    def supportVersion = '22.0.0'

    compile "com.android.support:support-v4:$supportVersion"
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"

    // Intellij annotations (NotNull, Nullable)
    compile "${rootProject.ext.intellijAnnotations}"

    compile "${rootProject.ext.guava}"

    compile "${rootProject.ext.retrofit}"
    compile "${rootProject.ext.retrofitJackson}"

    compile "${rootProject.ext.jackson}"

    compile "${rootProject.ext.okhttpUrlConnection}"
    compile "${rootProject.ext.okhttp}"

    compile "${rootProject.ext.rxJava}"
    compile 'io.reactivex:rxandroid:0.23.0'

    compile 'com.jakewharton:butterknife:6.0.0'

    compile "${rootProject.ext.dagger}"
    apt "${rootProject.ext.daggerCompiler}"
    provided 'org.glassfish:javax.annotation:10.0-b28'

    testCompile project(':debug')

    testCompile "${rootProject.ext.junit}"
    testCompile 'nl.jqno.equalsverifier:equalsverifier:1.5'
    testCompile "${rootProject.ext.assertJ}"
    testCompile "${rootProject.ext.mockito}"
}


android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.omricat.yacc"
        minSdkVersion 15
        targetSdkVersion 21
        versionCode rootProject.ext.versionCode
        versionName "${rootProject.ext.versionMajor}.${rootProject.ext.versionMinor}." +
                "${rootProject.ext.versionPatch}"
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        debug {

            versionNameSuffix "-${versionBuild}DEBUG"


        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility rootProject.ext.sourceCompatibility
        targetCompatibility rootProject.ext.targetCompatibility
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
    dexOptions {
        incremental false
    }
}

unMock {
    // URI to download the android-all.jar from. e.g. https://oss.sonatype.org/content/groups/public/org/robolectric/android-all/
    allAndroid =
            "file://${rootProject.projectDir}/android-all-4" +
                    ".3_r2-robolectric-0.jar"

    // classes to keep
    // matched by "startsWith" - you also need to include the dependencies manually
    // if you start with an "-" the class will match by "equals" (and it will additionally  match
    // inner classes of this class)
    keep = [
            "android.text.TextUtils"
    ]
}
